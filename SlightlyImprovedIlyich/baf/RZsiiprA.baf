IF
	!Allegiance(Myself,ENEMY)
	HaveSpell(CLERIC_ZONE_OF_SWEET_AIR)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
	OR(12)
		SpellCast([GOODCUTOFF.0.0.0.0.SUMMONED],WIZARD_STINKING_CLOUD)
		SpellCast([PC],WIZARD_STINKING_CLOUD)
		SpellCast([FAMILIAR],WIZARD_STINKING_CLOUD)
		SpellCast([ALLY],WIZARD_STINKING_CLOUD)
		SpellCast([CONTROLLED],WIZARD_STINKING_CLOUD)
		SpellCast([CHARMED],WIZARD_STINKING_CLOUD)
		SpellCast([GOODCUTOFF.0.0.0.0.SUMMONED],WIZARD_CLOUDKILL)
		SpellCast([PC],WIZARD_CLOUDKILL)
		SpellCast([FAMILIAR],WIZARD_CLOUDKILL)
		SpellCast([ALLY],WIZARD_CLOUDKILL)
		SpellCast([CONTROLLED],WIZARD_CLOUDKILL)
		SpellCast([CHARMED],WIZARD_CLOUDKILL)
THEN
	RESPONSE #100
		Enemy()
		EquipMostDamagingMelee()
		SetGlobal("RZ_See_Enemy","LOCALS",1)
		SetGlobal("RZ_Under_Attack","MYAREA",1)
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
		Continue()
END

IF
	!Allegiance(Myself,ENEMY)
	OR(19)
		Global("RZ_Under_Attack","MYAREA",1)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("RZ_See_Enemy","LOCALS",1)
		SetGlobal("RZ_Under_Attack","MYAREA",1)
		Continue()
END

IF
	Global("RZ_Can_Melee","LOCALS",0)
	!HaveSpell(CLERIC_COMMAND)
	!HaveSpell(CLERIC_POISON)
	!HaveSpell(CLERIC_MISCAST_MAGIC)
	!HaveSpell(CLERIC_MENTAL_DOMINATION)
	!HaveSpell(CLERIC_HOLD_PERSON)
	!HaveSpell(CLERIC_DOOM)
THEN
	RESPONSE #100
		SetGlobal("RZ_Can_Melee","LOCALS",1)
		Continue()
END

IF
        Global("RZ_Rest","LOCALS",0)
        OR(7)
             Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	     Detect([PC])
	     Detect([ALLY])
	     Detect([CONTROLLED])
             Detect([CHARMED])
             Detect([FAMILIAR])
             Detect(NearestEnemyOf(Myself))
 THEN
	RESPONSE #100
	         SetGlobal("RZ_Rest","LOCALS",1)
	         SetGlobalTimer("RZ_Rest_Timer","LOCALS",2400)
END

IF
	!GlobalTimerNotExpired("RZ_Rest_Timer","LOCALS")
	!Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	!Detect([PC])
	!Detect([ALLY])
	!Detect([CONTROLLED])
        !Detect([CHARMED])
        !Detect([FAMILIAR])
        !Detect(NearestEnemyOf(Myself))
        CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Rest_Timer","LOCALS",3600)
		SetGlobal("RZ_Rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	Global("RZ_See_Enemy","LOCALS",0)
	Allegiance(Myself,ENEMY)
	OR(20)
		Heard([ENEMY],100)
		See(NearestEnemyOf(Myself))
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		SetGlobal("RZ_See_Enemy","LOCALS",1)
		Shout(100)
		Continue()
END

IF
	!GlobalTimerNotExpired("RZ_Shout_Help_Timer","LOCALS")
	NumCreatureGT([ENEMY],1)
	OR(18)
                AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Shout_Help_Timer","LOCALS",6)
		Shout(101)
		Continue()
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN52",Myself) // Potion of Extra Healing
	HPPercentLT(Myself,65)
	!Name("RZsiiO03",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN52",Myself) // Potion of Extra Healing
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN08",Myself) // Potion of Healing
	HPPercentLT(Myself,70)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN08",Myself) // Potion of Healing
END

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
END

IF
	!See(NearestEnemyOf(Myself))
	Range(NearestDoor,8)
	OpenState(NearestDoor,FALSE)
THEN
	RESPONSE #100
		OpenDoor(NearestDoor)
		Continue()
END

IF
	Global("RZ_See_Enemy","LOCALS",0)
THEN
	RESPONSE #100
	         RandomWalk()
        RESPONSE #100
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_SANCTUARY)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
        CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,CLERIC_SANCTUARY)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN46",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Use_Potion_Timer","LOCALS",36)
		UseItem("POTN46",Myself) // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN14",Myself) // Oil of Speed
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN46",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN46") // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN14") // Oil of Speed
END

IF
	Global("RZ_Move_To_Enemy","LOCALS",1)
        Name("RZsiiD03",Myself) // Duergar
	Allegiance(Myself,ENEMY)
	OR(2)
	     ActionListEmpty()
	     Range([ENEMY.0.DWARF.FIGHTER_ALL],15)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_Enemy","LOCALS",0)
		ClearActions(Myself)
END

IF
	Global("RZ_Move_To_Enemy","LOCALS",0)
	Name("RZsiiD03",Myself) // Duergar
	OR(2)
		HaveSpell(CLERIC_DEFENSIVE_HARMONY)
		HaveSpell(CLERIC_STRENGTH_OF_ONE)
	Allegiance(Myself,ENEMY)
	NumCreatureGT([ENEMY.0.DWARF.FIGHTER_ALL],0)
	!Range([ENEMY.0.DWARF.FIGHTER_ALL],15)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_Enemy","LOCALS",1)
		MoveToObject([ENEMY.0.DWARF.FIGHTER_ALL])
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_ZONE_OF_SWEET_AIR)
	OR(2)
		SpellCast([GOODCUTOFF],WIZARD_STINKING_CLOUD)
		SpellCast([GOODCUTOFF],WIZARD_CLOUDKILL)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,CLERIC_ZONE_OF_SWEET_AIR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_DEFENSIVE_HARMONY)
	RandomNumGT(20,5)
	OR(2)
		!CheckStat(Myself,0,SANCTUARY)
		!GlobalTimerNotExpired("RZ_Defensive_Spell","LOCALS")
        See(NearestEnemyOf(Myself))
        Range([ENEMY],15)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Defensive_Spell","LOCALS",18)
		Spell(Myself,CLERIC_DEFENSIVE_HARMONY)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_STRENGTH_OF_ONE)
	RandomNumGT(20,5)
	OR(2)
		!CheckStat(Myself,0,SANCTUARY)
		!GlobalTimerNotExpired("RZ_Defensive_Spell","LOCALS")
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
        Range([ENEMY],15)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Defensive_Spell","LOCALS",18)
		Spell(Myself,CLERIC_STRENGTH_OF_ONE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_ARMOR_OF_FAITH)
	RandomNumGT(20,5)
	OR(2)
		!CheckStat(Myself,0,SANCTUARY)
		!GlobalTimerNotExpired("RZ_Defensive_Spell","LOCALS")
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Defensive_Spell","LOCALS",18)
		Spell(Myself,CLERIC_ARMOR_OF_FAITH)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_DRAW_UPON_HOLY_MIGHT)
	RandomNumGT(20,5)
	OR(2)
		!CheckStat(Myself,0,SANCTUARY)
		!GlobalTimerNotExpired("RZ_Defensive_Spell","LOCALS")
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Defensive_Spell","LOCALS",18)
		Spell(Myself,CLERIC_DRAW_UPON_HOLY_MIGHT)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(CLERIC_BLESS)
	RandomNumGT(20,10)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	See([ENEMY])
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREPRIEST)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(LastSeenBy(Myself),CLERIC_BLESS)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	OR(2)
		ActionListEmpty()
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",0)
		ClearActions(Myself)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	Or(2)
             !CheckStat(Player1,0,SANCTUARY)
	     StateCheck(Player1,STATE_INVISIBLE)
        Or(3)
             !CheckStat(Player2,0,SANCTUARY)
	     StateCheck(Player2,STATE_INVISIBLE)
	     HPLT(Player2,1)
        Or(3)
             !CheckStat(Player3,0,SANCTUARY)
	     StateCheck(Player3,STATE_INVISIBLE)
	     HPLT(Player3,1)
        Or(3)
             !CheckStat(Player4,0,SANCTUARY)
	     StateCheck(Player4,STATE_INVISIBLE)
	     HPLT(Player4,1)
        Or(3)
             !CheckStat(Player5,0,SANCTUARY)
	     StateCheck(Player5,STATE_INVISIBLE)
	     HPLT(Player5,1)
        Or(3)
             !CheckStat(Player6,0,SANCTUARY)
	     StateCheck(Player6,STATE_INVISIBLE)
	     HPLT(Player6,1)
      Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",0)
		ClearActions(Myself)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	Range(NearestDoor,30)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(NearestDoor)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
THEN
	RESPONSE #100
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(0)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player1,1)
	CheckStat(Player1,0,SANCTUARY)
	!StateCheck(Player1,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player1)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(1)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player2,1)
	CheckStat(Player2,0,SANCTUARY)
	!StateCheck(Player2,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player2)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(2)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player3,1)
	CheckStat(Player3,0,SANCTUARY)
	!StateCheck(Player3,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player3)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(3)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player4,1)
	CheckStat(Player4,0,SANCTUARY)
	!StateCheck(Player4,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player4)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(4)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player5,1)
	CheckStat(Player5,0,SANCTUARY)
	!StateCheck(Player5,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player5)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(5)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player6,1)
	CheckStat(Player6,0,SANCTUARY)
	!StateCheck(Player6,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player6)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
        !Global("RZ_See_Enemy","LOCALS",0)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
	         RandomWalk()
END

IF
	OR(2)
	     Global("RZ_Can_Melee","LOCALS",0)
	     Global("RZ_Ranged","LOCALS",0)
        !GlobalTimerNotExpired("RZ_Run_Away_Timer","LOCALS")
	RandomNumGT(20,5)
	CheckStat(Myself,0,SANCTUARY)
	Range(NearestEnemyOf(Myself),8)
THEN
	RESPONSE #100
                 EquipMostDamagingMelee()
	         SetGlobalTimer("RZ_Run_Away_Timer","LOCALS",12)
	         RunAwayFrom(NearestEnemyOf(Myself),60)
END

IF
        Global("RZ_Ranged","LOCALS",0)
        !HasItem("BULL01",Myself)
THEN
	RESPONSE #100
              SetGlobal("RZ_Ranged","LOCALS",1)
END