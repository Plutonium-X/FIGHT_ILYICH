IF
        Global("RZ_Create_Scroll","LOCALS",0)
        Or(2)
             Name("RZsiiT01",Myself)
             Name("RZsiiT04",Myself)
THEN
    RESPONSE #100        
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL7I",Myself,1,1,0)        //death spell
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL7S",Myself,1,1,0)        //chain light
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL8B",Myself,1,1,0)        //nishruu
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL7W",Myself,1,1,0)        //wyvern call
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL7E",Myself,1,1,0)        //inv. stalker
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             Continue()
END

IF
        Global("RZ_Create_Scroll","LOCALS",0)
        Or(2)
             Name("RZsiiD10",Myself)
             Name("RZsiiH04",Myself)
THEN
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL2B",Myself,1,1,0)        //stoneskin
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL6R",Myself,1,1,0)        //spider spawn
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL2A",Myself,1,1,0)        //mon. sum. II
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL5L",Myself,1,1,0)        //polym. other
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRLA8",Myself,1,1,0)        //contagion
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             Continue()
END

IF
        Global("RZ_Create_Scroll","LOCALS",0)
        OR(2)
        Name("RZsiiD05",Myself)
        Name("CLONE1",Myself)
THEN
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL5P",Myself,1,1,0)        //chaos
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRLAR",Myself,1,1,0)        //sunfire
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL2F",Myself,1,1,0)        //cone of cold
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL5Q",Myself,1,1,0)        //feeblemind
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             GiveItemCreate("SCRL2G",Myself,1,1,0)        //mon. sum III
             Continue()
    RESPONSE #100
             SetGlobal("RZ_Create_Scroll","LOCALS",1)
             Continue()
END


IF
	!Allegiance(Myself,ENEMY)
	OR(19)
		Global("RZ_Under_Attack","MYAREA",1)
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("RZ_See_Enemy","LOCALS",1)
		SetGlobal("RZ_Under_Attack","MYAREA",1)
		Continue()
END

IF
	Global("RZ_Can_Melee","LOCALS",0)
        !HaveSpell(WIZARD_SUN_FIRE)
        !HaveSpell(WIZARD_BURNING_HANDS)
	!HaveSpell(WIZARD_BLINDNESS)
	!HaveSpell(WIZARD_SPOOK)
        !HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
	!HaveSpell(WIZARD_MAGIC_MISSILE)
	!HaveSpell(WIZARD_CHROMATIC_ORB)
	!HasItem("SCRL77",Myself) // Magic Missile
	!HaveSpell(WIZARD_FLAME_ARROW)
	!HasItem("SCRL1F",Myself) // Flame Arrow
	!HaveSpell(WIZARD_MELF_ACID_ARROW)
	!HaveSpell(WIZARD_SKULL_TRAP)
	!HaveSpell(WIZARD_RAY_OF_ENFEEBLEMENT)
	!HaveSpell(WIZARD_HORROR)
	!HaveSpell(WIZARD_HOLD_PERSON)
        !HasItem("SCRL1I",Myself) // Hold Person
	!HaveSpell(WIZARD_CHAOS)
	!HaveSpell(WIZARD_EMOTION_HOPELESSNESS)
	!HaveSpell(WIZARD_CONFUSION)
	!HasItem("SCRL1O",Myself) // Slow
	!HaveSpell(WIZARD_SLOW)
	!HaveSpell(WIZARD_GLITTERDUST)
	!HaveSpell(WIZARD_GREATER_MALISON)
THEN
	RESPONSE #100
		SetGlobal("RZ_Can_Melee","LOCALS",1)
		Continue()
END

IF
        Global("RZ_Rest","LOCALS",0)
        OR(7)
             Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	     Detect([PC])
	     Detect([ALLY])
	     Detect([CONTROLLED])
             Detect([CHARMED])
             Detect([FAMILIAR])
             Detect(NearestEnemyOf(Myself))
 THEN
	RESPONSE #100
	         SetGlobal("RZ_Rest","LOCALS",1)
	         SetGlobalTimer("RZ_Rest_Timer","LOCALS",2400)
END

IF
	!GlobalTimerNotExpired("RZ_Rest_Timer","LOCALS")
	!Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	!Detect([PC])
	!Detect([ALLY])
	!Detect([CONTROLLED])
        !Detect([CHARMED])
        !Detect([FAMILIAR])
        !Detect(NearestEnemyOf(Myself))
        CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Rest_Timer","LOCALS",3600)
		SetGlobal("RZ_Rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	Global("RZ_See_Enemy","LOCALS",0)
	Allegiance(Myself,ENEMY)
	OR(20)
		Heard([ENEMY],100)
		See(NearestEnemyOf(Myself))
		AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		SetGlobal("RZ_See_Enemy","LOCALS",1)
		Shout(100)
		Continue()
END

IF
	!GlobalTimerNotExpired("RZ_Shout_Help_Timer","LOCALS")
	NumCreatureGT([ENEMY],1)
	OR(18)
                AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Shout_Help_Timer","LOCALS",6)
		Shout(101)
		Continue()
END

IF
	HasItem("SCRLA5",Myself)
THEN
	RESPONSE #100
		UseItem("SCRLA5",Myself)
		Continue()
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_VOCALIZE)
	StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_VOCALIZE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_STONE_SKIN)
	Or(2)
             !StateCheck(Myself,STATE_INVISIBLE)
             CheckStatLT(Myself,1,STONESKINS)
        CheckStatLT(Myself,2,STONESKINS)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_STONE_SKIN)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("SCRL2B",Myself)
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
        CheckStatLT(Myself,1,STONESKINS)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("SCRL2B",Myself)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_ARMOR)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_ARMOR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	Global("RZ_Minor_Spell_Trigger","LOCALS",0)
	!HaveSpell(WIZARD_STONE_SKIN)
	Class(Myself,MAGE_ALL)
	LevelGT(Myself,6)
	CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	CheckStatLT(Myself,2,STONESKINS)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobal("RZ_Minor_Spell_Trigger","LOCALS",1)
		DisplayString(Myself,39968) // Spell Trigger - Fired
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_BLUR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_SHADOW_DOOR)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_SHADOW_DOOR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("SCRL1Y",Myself) // Improved Invisibility
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("SCRL1Y",Myself) // Improved Invisibility
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN52",Myself) // Potion of Extra Healing
	HPPercentLT(Myself,50)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN52",Myself) // Potion of Extra Healing
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN08",Myself) // Potion of Healing
	HPPercentLT(Myself,65)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN08",Myself) // Potion of Healing
END

IF
	!Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
END

IF
	!See(NearestEnemyOf(Myself))
	Range(NearestDoor,8)
	OpenState(NearestDoor,FALSE)
THEN
	RESPONSE #100
		OpenDoor(NearestDoor)
		Continue()
END

IF
	Global("RZ_See_Enemy","LOCALS",0)
THEN
	RESPONSE #100
	         RandomWalk()
        RESPONSE #100
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN24",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Use_Potion_Timer","LOCALS",60)
		UseItem("POTN24",Myself) // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN35",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Use_Potion_Timer","LOCALS",60)
		UseItem("POTN35",Myself) // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN46",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Use_Potion_Timer","LOCALS",60)
		UseItem("POTN46",Myself) // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN14",Myself) // Oil of Speed
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN46",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN46") // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN35",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN35") // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN24",Myself) // Potion of Stone Form
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN24") // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
	OR(2)
		See(NearestEnemyOf(Myself))
		Global("RZ_Move_To_PC","LOCALS",1)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		DestroyItem("POTN14") // Oil of Speed
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_GHOST_ARMOR)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_GHOST_ARMOR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_SHIELD)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_SHIELD)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_MINOR_SPELL_DEFLECTION)
	!StateCheck(Myself,STATE_IMPROVEDINVISIBILITY)
	Allegiance(Myself,ENEMY)
	OR(4)
		Detect([PC.0.0.MAGE_ALL])
		Detect([PC.0.0.BARD_ALL])
		Detect([PC.0.0.DRUID_ALL])
		Detect([PC.0.0.CLERIC_ALL])
        CheckStat(Myself,0,WIZARD_SPELL_DEFLECTION)
        !StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_MINOR_SPELL_DEFLECTION)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_MIRROR_IMAGE)
	!StateCheck(Myself,STATE_MIRRORIMAGE)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
        CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_MIRROR_IMAGE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_HASTE)
	!StateCheck(Myself,STATE_HASTED)
	Or(2)
	     Global("RZ_Move_To_PC","LOCALS",1)
	     See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
THEN
	RESPONSE #100
	        EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_HASTE)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	OR(2)
		ActionListEmpty()
		See(NearestEnemyOf(Myself))
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",0)
		ClearActions(Myself)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	Or(2)
             !CheckStat(Player1,0,SANCTUARY)
	     StateCheck(Player1,STATE_INVISIBLE)
        Or(3)
             !CheckStat(Player2,0,SANCTUARY)
	     StateCheck(Player2,STATE_INVISIBLE)
	     HPLT(Player2,1)
        Or(3)
             !CheckStat(Player3,0,SANCTUARY)
	     StateCheck(Player3,STATE_INVISIBLE)
	     HPLT(Player3,1)
        Or(3)
             !CheckStat(Player4,0,SANCTUARY)
	     StateCheck(Player4,STATE_INVISIBLE)
	     HPLT(Player4,1)
        Or(3)
             !CheckStat(Player5,0,SANCTUARY)
	     StateCheck(Player5,STATE_INVISIBLE)
	     HPLT(Player5,1)
        Or(3)
             !CheckStat(Player6,0,SANCTUARY)
	     StateCheck(Player6,STATE_INVISIBLE)
	     HPLT(Player6,1)
      Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",0)
		ClearActions(Myself)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
	Range(NearestDoor,30)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToObject(NearestDoor)
END

IF
	Global("RZ_Move_To_PC","LOCALS",1)
THEN
	RESPONSE #100
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(0)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player1,1)
	CheckStat(Player1,0,SANCTUARY)
	!StateCheck(Player1,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player1)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(1)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player2,1)
	CheckStat(Player2,0,SANCTUARY)
	!StateCheck(Player2,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player2)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(2)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player3,1)
	CheckStat(Player3,0,SANCTUARY)
	!StateCheck(Player3,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player3)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(3)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player4,1)
	CheckStat(Player4,0,SANCTUARY)
	!StateCheck(Player4,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player4)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(4)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player5,1)
	CheckStat(Player5,0,SANCTUARY)
	!StateCheck(Player5,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player5)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
	Global("RZ_See_Enemy","LOCALS",1)
	NumInPartyGT(5)
	!See(NearestEnemyOf(Myself))
	!HPLT(Player6,1)
	CheckStat(Player6,0,SANCTUARY)
	!StateCheck(Player6,STATE_INVISIBLE)
	Allegiance(Myself,ENEMY)
THEN
	RESPONSE #100
		SetGlobal("RZ_Move_To_PC","LOCALS",1)
		MoveToObject(Player6)
END

IF
	Global("RZ_Move_To_PC","LOCALS",0)
        !Global("RZ_See_Enemy","LOCALS",0)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
	         RandomWalk()
END

IF
	OR(2)
	     Global("RZ_Can_Melee","LOCALS",0)
	     Global("RZ_Ranged","LOCALS",0)
        !GlobalTimerNotExpired("RZ_Run_Away_Timer","LOCALS")
	RandomNumGT(20,5)
	!StateCheck(Myself,STATE_INVISIBLE)
	Range(NearestEnemyOf(Myself),8)
THEN
	RESPONSE #100
                 EquipMostDamagingMelee()
	         SetGlobalTimer("RZ_Run_Away_Timer","LOCALS",12)
	         RunAwayFrom(NearestEnemyOf(Myself),60)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_SUN_FIRE)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	Range(NearestEnemyOf(Myself),15)
	!Range([ENEMY],15)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_SUN_FIRE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("SCRLAR",Myself)
	RandomNumGT(20,15)
	Allegiance(Myself,ENEMY)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
	Range(NearestEnemyOf(Myself),15)
	!Range([ENEMY],15)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("SCRLAR",Myself)
END