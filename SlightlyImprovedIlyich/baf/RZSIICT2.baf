IF
	Global("RZ_Can_Melee","LOCALS",0)
        !HaveSpell(WIZARD_SUN_FIRE)
        !HaveSpell(WIZARD_BURNING_HANDS)
	!HaveSpell(WIZARD_BLINDNESS)
	!HaveSpell(WIZARD_SPOOK)
        !HaveSpell(WIZARD_AGANNAZAR_SCORCHER)
	!HaveSpell(WIZARD_MAGIC_MISSILE)
	!HaveSpell(WIZARD_CHROMATIC_ORB)
	!HasItem("SCRL77",Myself) // Magic Missile
	!HaveSpell(WIZARD_FLAME_ARROW)
	!HasItem("SCRL1F",Myself) // Flame Arrow
	!HaveSpell(WIZARD_MELF_ACID_ARROW)
	!HaveSpell(WIZARD_SKULL_TRAP)
	!HaveSpell(WIZARD_RAY_OF_ENFEEBLEMENT)
	!HaveSpell(WIZARD_HORROR)
	!HaveSpell(WIZARD_DIRE_CHARM)
        !HasItem("SCRL1I",Myself) // Hold Person
	!HaveSpell(WIZARD_CHAOS)
	!HaveSpell(WIZARD_EMOTION_HOPELESSNESS)
	!HaveSpell(WIZARD_CONFUSION)
	!HasItem("SCRL1O",Myself) // Slow
	!HaveSpell(WIZARD_SLOW)
	!HaveSpell(WIZARD_GLITTERDUST)
	!HaveSpell(WIZARD_GREATER_MALISON)
THEN
	RESPONSE #100
		SetGlobal("RZ_Can_Melee","LOCALS",1)
END

IF
        Global("RZ_Ranged","LOCALS",0)
        !HasItem("MELFMET",Myself)
        !HasItem("AROW02",Myself)
        !HasItem("AROW01",Myself)
        !HasItem("DAGG16",Myself)
THEN
	RESPONSE #100
              SetGlobal("RZ_Ranged","LOCALS",1)
END

IF
        Global("RZ_Rest","LOCALS",0)
        OR(7)
             Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	     Detect([PC])
	     Detect([ALLY])
	     Detect([CONTROLLED])
             Detect([CHARMED])
             Detect([FAMILIAR])
             Detect(NearestEnemyOf(Myself))
 THEN
	RESPONSE #100
	         SetGlobal("RZ_Rest","LOCALS",1)
	         SetGlobalTimer("RZ_Rest_Timer","LOCALS",2400)
END

IF
	!GlobalTimerNotExpired("RZ_Rest_Timer","LOCALS")
	!Detect([GOODCUTOFF.0.0.0.0.SUMMONED])
	!Detect([PC])
	!Detect([ALLY])
	!Detect([CONTROLLED])
        !Detect([CHARMED])
        !Detect([FAMILIAR])
        !Detect(NearestEnemyOf(Myself))
        CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Rest_Timer","LOCALS",3600)
		SetGlobal("RZ_Rest","LOCALS",0)
		ApplySpell(Myself,RESTORE_FULL_HEALTH)
		Rest()
END

IF
	Class(Myself,MAGE_ALL)
        !Allegiance(Myself,ENEMY)
	Global("RZ_SII_Cutscene_2","MYAREA",2)
	!See([PC])
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIIGEN",OVERRIDE)
		ChangeAIScript("RZSIIMGA",CLASS)
		ChangeAIScript("RZSIIMGB",RACE)
		ChangeAIScript("RZSIIATA",GENERAL)
END

IF
	!Class(Myself,MAGE_ALL)
        !Allegiance(Myself,ENEMY)
	Global("RZ_SII_Cutscene_2","MYAREA",2)
	!See([PC])
THEN
	RESPONSE #100
		Enemy()
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIIGEN",OVERRIDE)
		ChangeAIScript("RZSIITHA",CLASS)
		ChangeAIScript("RZSIIATT",RACE)
END


IF
	Class(Myself,MAGE_ALL)
        !Allegiance(Myself,ENEMY)
	OR(19)
		Global("RZ_SII_Cutscene_2","MYAREA",2)
                AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
		DisplayStringHead(Myself,61895)
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIIMGA",OVERRIDE)
		ChangeAIScript("RZSIIMGB",CLASS)
		ChangeAIScript("RZSIIATA",RACE)
	RESPONSE #100
		Enemy()
		DisplayStringHead(Myself,61896)
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIIMGA",OVERRIDE)
		ChangeAIScript("RZSIIMGB",CLASS)
		ChangeAIScript("RZSIIATA",RACE)
END

IF
	!Class(Myself,MAGE_ALL)
        !Allegiance(Myself,ENEMY)
	OR(19)
		Global("RZ_SII_Cutscene_2","MYAREA",2)
                AttackedBy([GOODCUTOFF.0.0.0.0.SUMMONED],DEFAULT)
		AttackedBy([PC],DEFAULT)
		AttackedBy([FAMILIAR],DEFAULT)
		AttackedBy([ALLY],DEFAULT)
		AttackedBy([CONTROLLED],DEFAULT)
		AttackedBy([CHARMED],DEFAULT)
		HitBy([GOODCUTOFF.0.0.0.0.SUMMONED],CRUSHING)
		HitBy([PC],CRUSHING)
		HitBy([FAMILIAR],CRUSHING)
		HitBy([ALLY],CRUSHING)
		HitBy([CONTROLLED],CRUSHING)
		HitBy([CHARMED],CRUSHING)
		SpellCastOnMe([GOODCUTOFF.0.0.0.0.SUMMONED],0)
		SpellCastOnMe([PC],0)
		SpellCastOnMe([FAMILIAR],0)
		SpellCastOnMe([ALLY],0)
		SpellCastOnMe([CONTROLLED],0)
		SpellCastOnMe([CHARMED],0)
THEN
	RESPONSE #100
		Enemy()
		DisplayStringHead(Myself,61886)
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIITHA",OVERRIDE)
		ChangeAIScript("RZSIIATT",CLASS)
	RESPONSE #100
		Enemy()
		DisplayStringHead(Myself,61887)
		SetGlobal("RZ_SII_Cutscene_2","MYAREA",2)
		ChangeAIScript("RZSIITHA",OVERRIDE)
		ChangeAIScript("RZSIIATT",CLASS)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_VOCALIZE)
	StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_VOCALIZE)
END

IF
	HasItem("SCRLA5",Myself)
THEN
	RESPONSE #100
		UseItem("SCRLA5",Myself)
END

IF
  Global("RZ_SII_Cutscene_2","MYAREA",0)
THEN
    RESPONSE #100
END

IF
	Class(Myself,MAGE_ALL)
        !GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	Global("RZ_Minor_Spell_Trigger","LOCALS",0)
	!HaveSpell(WIZARD_STONE_SKIN)
	CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
	CheckStatLT(Myself,2,STONESKINS)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobal("RZ_Minor_Spell_Trigger","LOCALS",1)
		DisplayString(Myself,39968) // Spell Trigger - Fired
		ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself,WIZARD_BLUR)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_STONE_SKIN)
	Or(2)
             !StateCheck(Myself,STATE_INVISIBLE)
             CheckStatLT(Myself,1,STONESKINS)
        CheckStatLT(Myself,2,STONESKINS)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_STONE_SKIN)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN52",Myself) // Potion of Extra Healing
	HPPercentLT(Myself,60)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN52",Myself) // Potion of Extra Healing
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN24",Myself) // Potion of Stone Form
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Use_Potion_Timer","LOCALS",30)
		UseItem("POTN24",Myself) // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
	OR(2)
		RandomNumGT(20,10)
		DifficultyGT(EASY)
	OR(2)
		RandomNumGT(20,15)
		DifficultyGT(EASIEST)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		UseItem("POTN14",Myself) // Oil of Speed
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HasItem("POTN24",Myself) // Potion of Stone Form
THEN
	RESPONSE #100
		DestroyItem("POTN24") // Potion of Stone Form
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Use_Potion_Timer","LOCALS")
	HasItem("POTN14",Myself) // Oil of Speed
THEN
	RESPONSE #100
		DestroyItem("POTN14") // Oil of Speed
END

IF

        OR(2)
        Global("RZ_Can_Melee","LOCALS",0)
        Global("RZ_Ranged","LOCALS",0)
        !GlobalTimerNotExpired("RZ_Run_Away_Timer","LOCALS")
	RandomNumGT(20,5)
	!StateCheck(Myself,STATE_INVISIBLE)
	Range([0.UNDEAD],8)
THEN
	RESPONSE #100
                 EquipMostDamagingMelee()
	         SetGlobalTimer("RZ_Run_Away_Timer","LOCALS",12)
	         RunAwayFrom(NearestEnemyOf(Myself),45)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_SLOW)
	RandomNumGT(20,5)
	See([0.UNDEAD])
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_SLOW)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_FLAME_ARROW)
	RandomNumGT(20,10)
	See([0.UNDEAD])
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_MELF_ACID_ARROW)
	RandomNumGT(20,10)
	See([0.UNDEAD])
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_MAGIC_MISSILE)
	RandomNumGT(20,10)
	See([0.UNDEAD])
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	HaveSpell(WIZARD_SUN_FIRE)
	RandomNumGT(20,10)
	!Range([NEUTRAL.0.0.FIGHTER_THIEF],15)
	Range([0.UNDEAD],15)
	!StateCheck(Myself,STATE_SILENCED)
	CheckStatLT(Myself,20,SPELLFAILUREMAGE)
	CheckStat(Myself,0,CLERIC_INSECT_PLAGUE)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		Spell(Myself,WIZARD_SUN_FIRE)
END

IF
	!GlobalTimerNotExpired("RZ_Action_Timer","LOCALS")
	!GlobalTimerNotExpired("RZ_Invisibility_Potion_Timer","LOCALS")
	HasItem("POTN10",Myself) // Potion of Invisibility
	!StateCheck(Myself,STATE_INVISIBLE)
	See([0.UNDEAD])
	Range(LastSeenBy(Myself),12)
	CheckStat([PC],0,TRUE_SIGHT)
	CheckStat(SecondNearest([PC]),0,TRUE_SIGHT)
	CheckStat(ThirdNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FourthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(FifthNearest([PC]),0,TRUE_SIGHT)
	CheckStat(SixthNearest([PC]),0,TRUE_SIGHT)
THEN
	RESPONSE #100
		SetGlobalTimer("RZ_Action_Timer","LOCALS",6)
		SetGlobalTimer("RZ_Invisibility_Potion_Timer","LOCALS",12)
		UseItem("POTN10",Myself) // Potion of Invisibility
END

IF
	ActionListEmpty()
	!Class(Myself,MAGE_ALL)
	Global("RZ_Backstab","LOCALS",0)
	StateCheck(Myself,STATE_INVISIBLE)
	See([0.UNDEAD])
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobal("RZ_Backstab","LOCALS",1)
		FollowObjectFormation(LastSeenBy(Myself),11,1)
END

IF
        Delay(1)
        Global("RZ_Backstab","LOCALS",1)
	See([0.UNDEAD])
	Range(LastSeenBy(Myself),6)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		SetGlobal("RZ_Backstab","LOCALS",0)
		AttackOneRound(LastSeenBy(Myself))
END

IF
        Global("RZ_Ranged","LOCALS",0)
        OR(2)
	     ActionListEmpty()
	     Global("RZ_Can_Melee","LOCALS",1)
        !RandomNum(21,6)
        OR(2)
	     Global("RZ_Can_Melee","LOCALS",1)
	     !StateCheck(Myself,STATE_INVISIBLE)
	See([0.UNDEAD])
	!Range(LastSeenBy(Myself),6)
THEN
	RESPONSE #100
		EquipRanged()
		AttackReevaluate(LastSeenBy(Myself),30)
END

IF
        OR(2)
	     ActionListEmpty()
	     Global("RZ_Can_Melee","LOCALS",1)
        !RandomNum(21,6)
	OR(2)
	     Global("RZ_Can_Melee","LOCALS",1)
	     !StateCheck(Myself,STATE_INVISIBLE)
	See([0.UNDEAD])
	OR(2)
	     Global("RZ_Can_Melee","LOCALS",1)
	     Range(LastSeenBy(Myself),5)
THEN
	RESPONSE #100
		EquipMostDamagingMelee()
		AttackReevaluate(LastSeenBy(Myself),30)
END

IF
	ActionListEmpty()
THEN
	RESPONSE #100
		RandomWalk()
END